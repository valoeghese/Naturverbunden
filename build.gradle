plugins {
	id "fabric-loom" version "0.6-SNAPSHOT" apply false
	id "maven-publish"
	id "org.ajoberstar.grgit" version "3.1.1"
}

if(rootProject.file("private.gradle").exists()) { //Publishing details
	apply from: "private.gradle"
}

def getSubprojectVersion(project) {
	def version = "${project.mod_version}"

	if (grgit == null) {
		return version + "+nogit"
	}
	def latestCommits = grgit.log(paths: [project.name], maxCommits: 1)
	if (latestCommits.isEmpty()) {
		return version + "+uncommited"
	}

	return version + "+" + latestCommits.get(0).id.substring(0, 8) + DigestUtils.sha256Hex(Globals.mcVersion).substring(0, 2)
}

allprojects {
	apply plugin: "fabric-loom"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	archivesBaseName = "${project.archives_base_name}"
	group = "${project.maven_group}"
	version = "${project.mod_version}+${project.minecraft_version}"

	repositories {
		maven { url "https://server.bbkr.space/artifactory/libs-release" } // Cotton maven - home of Cotton projects
		maven { url "https://maven.abusedmaster.xyz" } // NerdHub maven
		maven {
			name = "Onyx Studios"
			url = "https://maven.onyxstudios.dev"
		}
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
		implementation "tk.valoeghese:ZoesteriaConfig:${project.zoesteria_config_version}"
		include "tk.valoeghese:ZoesteriaConfig:${project.zoesteria_config_version}"

		// Bad Bloat Api
		modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:3.0.0-nightly.21w06a"
		modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:3.0.0-nightly.21w06a"
		include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:3.0.0-nightly.21w06a"
		include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:3.0.0-nightly.21w06a"

		compileOnly "com.google.code.findbugs:jsr305:3.0.2"
	}

	processResources {
		inputs.property "version", project.version
		from(sourceSets.main.resources.srcDirs) {
			include "fabric.mod.json"
			expand version: project.version
		}
		from(sourceSets.main.resources.srcDirs) {
			exclude "fabric.mod.json"
		}
	}

	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this task, sources will not be generated.
	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveClassifier = "sources"
		from sourceSets.main.allSource
	}

	jar {
		from "LICENSE"
	}
}

// configure the maven publication
publishing {
	publications {
		maven(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				classifier null
				builtBy remapJar
			}

			/*artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}-dev.jar") { //release jar - file location not provided anywhere in loom
				classifier "dev"
				builtBy remapJar
			}*/

			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
