plugins {
	id "java"
	id "fabric-loom" version "0.5.43" apply false
	id "maven-publish"
}

//apply plugin: "fabric-loom"

if(rootProject.file("private.gradle").exists()) { //Publishing details
	apply from: "private.gradle"
}

def getSubprojectVersion(project) {
	return "${project.mod_version}"
}

dependencies {
	implementation project(":naturverbunden-core")
	//include project(":naturverbunden-core")
	
	implementation project(":naturverbunden-health")
	//include project(":naturverbunden-health")
	
	implementation project(":naturverbunden-primitive")
	//include project(":naturverbunden-primitive")
	
	implementation project(":naturverbunden-terrain")
	//include project(":naturverbunden-terrain")
}

allprojects {
	apply plugin: "fabric-loom"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	archivesBaseName = "${project.archives_base_name}"
	group = "${project.maven_group}"
	version = "${project.mod_version}+${project.minecraft_version}"

	repositories {
		maven { url "https://server.bbkr.space/artifactory/libs-release" } // Cotton maven - home of Cotton projects
		maven { url "https://maven.abusedmaster.xyz" } // NerdHub maven
		maven {
			name = "Onyx Studios"
			url = "https://maven.onyxstudios.dev"
		}
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

		compileOnly "com.google.code.findbugs:jsr305:3.0.2"
	}

	processResources {
		inputs.property "version", project.version
		from(sourceSets.main.resources.srcDirs) {
			include "fabric.mod.json"
			expand version: project.version
		}
		from(sourceSets.main.resources.srcDirs) {
			exclude "fabric.mod.json"
		}
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveClassifier = "sources"
		from sourceSets.main.allSource
	}

	jar {
		from "LICENSE"
	}
	
	loom {
		shareCaches = true
	}
}

// configure the maven publication
publishing {
	publications {
		maven(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				classifier null
				builtBy remapJar
			}

			/*artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}-dev.jar") { //release jar - file location not provided anywhere in loom
				classifier "dev"
				builtBy remapJar
			}*/

			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
